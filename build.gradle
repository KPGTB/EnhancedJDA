plugins {
    id "java-library"

    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "io.freefair.lombok" version "8.11"

    id "maven-publish"
    id 'org.jreleaser' version '1.16.0'
}

group = "dev.projectenhanced"
version = "1.0.2"
description = 'EnhancedJDA is an advanced library that enhances the Java Discord API for creating powerful Discord bots'

repositories {
    mavenCentral()
}

dependencies {
    api "net.dv8tion:JDA:5.2.2"
    api 'io.github.cdimascio:dotenv-java:3.1.0'
    api "com.j256.ormlite:ormlite-jdbc:6.1"
    api 'com.google.code.gson:gson:2.11.0'
    api "ch.qos.logback:logback-classic:1.5.15"

    implementation 'org.xerial:sqlite-jdbc:3.47.1.0'
    implementation 'com.mysql:mysql-connector-j:9.1.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
}

java {
    withSourcesJar()
    withJavadocJar()

    sourceCompatibility = 17
    targetCompatibility = 17
}

shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
}

shadowJar.finalizedBy javadoc
shadowJar.finalizedBy sourcesJar

publishing {
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = 'dev.projectenhanced'
            artifactId = 'EnhancedJDA'

            from components.java

            pom {
                name = 'EnhancedJDA'
                description = 'EnhancedJDA is an advanced library that enhances the Java Discord API for creating powerful Discord bots'
                url = 'https://projectenhanced.dev'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'kpgtb'
                        name = 'KPG-TB'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/KPGTB/EnhancedJDA.git'
                    developerConnection = 'scm:git:ssh://github.com/KPGTB/EnhancedJDA.git'
                    url = 'https://github.com/KPGTB/EnhancedJDA'
                }
            }
        }
    }
}

jreleaser {
    release{
        github {
            skipRelease = true
            skipTag = true
        }
    }
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = 'C:\\Users\\kokkr\\.gnupg\\public.pgp'
        secretKey = 'C:\\Users\\kokkr\\.gnupg\\private.pgp'
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}